// Copyright Epic Games, Inc. All Rights Reserved.

#ifndef __QUAT_ISPH__
#define __QUAT_ISPH__

#include "Math/Vector.isph"
#include "Math/Matrix.isph"

inline uniform FVector4 MatrixToQuat(const uniform FMatrix &M)
{
	// If Matrix is NULL, return Identity quaternion. If any of them is 0, you won't be able to construct rotation
	// if you have two plane at least, we can reconstruct the frame using cross product, but that's a bit expensive op to do here
	// for now, if you convert to matrix from 0 scale and convert back, you'll lose rotation. Don't do that.
	if (IsAnyMatrixScaledAxesNearlyZero(M, KINDA_SMALL_NUMBER))
	{
		return Float0001;
	}

	//const MeReal *const t = (MeReal *) tm;
	uniform float	s;

	// Check diagonal (trace)
	const uniform float tr = M.M[0] + M.M[5] + M.M[10];

	if (tr > 0.0f)
	{
		uniform FVector4 qt;
		const uniform float InvS = 1.0f / sqrt(tr + 1.f);
		s = 0.5f * InvS;

		qt.V[0] = (M.M[6] - M.M[9]) * s;
		qt.V[1] = (M.M[8] - M.M[2]) * s;
		qt.V[2] = (M.M[1] - M.M[4]) * s;
		qt.V[3] = 0.5f * (1.f / InvS);

		return qt;
	}
	else
	{
		// diagonal is negative
		uniform int i = 0;

		if (M.M[5] > M.M[0])
		{
			i = 1;
		}

		if (M.M[10] > M.M[4*i+i])
		{
			i = 2;
		}

		static const uniform int nxt[3] = { 1, 2, 0 };
		const uniform int j = nxt[i];
		const uniform int k = nxt[j];

		s = M.M[4*i+i] - M.M[4*j+j] - M.M[4*k+k] + 1.0f;

		const uniform float InvS = 1.0f / sqrt(s);
		s = 0.5f * InvS;

		uniform FVector4 qt;

		qt.V[i] = 0.5f * (1.f / InvS);
		qt.V[j] = (M.M[4*i+j] + M.M[4*j+i]) * s;
		qt.V[k] = (M.M[4*i+k] + M.M[4*k+i]) * s;
		qt.V[3] = (M.M[4*j+k] - M.M[4*k+j]) * s;

		return qt;
	}
}

/**
* Multiplies two quaternions; the order matters.
*
* Order matters when composing quaternions: C = VectorQuaternionMultiply2(A, B) will yield a quaternion C = A * B
* that logically first applies B then A to any subsequent transformation (right first, then left).
*
* @param Quat1	Pointer to the first quaternion
* @param Quat2	Pointer to the second quaternion
* @return Quat1 * Quat2
*/
inline FVector4 VectorQuaternionMultiply2( const FVector4& Quat1, const FVector4& Quat2 )
{
	FVector4 Result = VectorReplicate(Quat1, 3) * Quat2;
	Result = VectorMultiplyAdd((VectorReplicate(Quat1, 0) * VectorSwizzle(Quat2, 3,2,1,0)), QMULTI_SIGN_MASK0, Result);
	Result = VectorMultiplyAdd((VectorReplicate(Quat1, 1) * VectorSwizzle(Quat2, 2,3,0,1)), QMULTI_SIGN_MASK1, Result);
	Result = VectorMultiplyAdd((VectorReplicate(Quat1, 2) * VectorSwizzle(Quat2, 1,0,3,2)), QMULTI_SIGN_MASK2, Result);

	return Result;
}

inline uniform FVector4 VectorQuaternionMultiply2( const uniform FVector4& Quat1, const uniform FVector4& Quat2 )
{
	uniform FVector4 Result = VectorReplicate(Quat1, 3) * Quat2;
	Result = VectorMultiplyAdd((VectorReplicate(Quat1, 0) * VectorSwizzle(Quat2, 3,2,1,0)), QMULTI_SIGN_MASK0, Result);
	Result = VectorMultiplyAdd((VectorReplicate(Quat1, 1) * VectorSwizzle(Quat2, 2,3,0,1)), QMULTI_SIGN_MASK1, Result);
	Result = VectorMultiplyAdd((VectorReplicate(Quat1, 2) * VectorSwizzle(Quat2, 1,0,3,2)), QMULTI_SIGN_MASK2, Result);

	return Result;
}

// Normalize quaternion ( result = (Q.Q >= 1e-8) ? (Q / |Q|) : (0,0,0,1) )
inline FVector4 VectorNormalizeQuaternion(const FVector4& UnnormalizedQuat)
{
	return VectorNormalizeSafe(UnnormalizedQuat, Float0001);
}

inline uniform FVector4 VectorNormalizeQuaternion(const uniform FVector4& UnnormalizedQuat)
{
	return VectorNormalizeSafe2(UnnormalizedQuat, Float0001);
}

inline FVector4 QuatInverse(const FVector4 &Quat)
{
	return Quat * QINV_SIGN_MASK;
}

inline uniform FVector4 QuatInverse(const uniform FVector4 &Quat)
{
	return Quat * QINV_SIGN_MASK;
}

inline FVector4 QuatFastLerp(const FVector4& A, const FVector4& B, const float Alpha)
{
	// To ensure the 'shortest route', we make sure the dot product between the both rotations is positive.
	const float DotResult = VectorDot(A, B);
	const float Bias = select(DotResult >= 0.f, 1.0f, -1.0f);
	return (B * Alpha) + (A * (Bias * (1.f - Alpha)));
}

inline uniform FVector4 QuatFastLerp(const uniform FVector4& A, const uniform FVector4& B, const uniform float Alpha)
{
	// To ensure the 'shortest route', we make sure the dot product between the both rotations is positive.
	const uniform float DotResult = VectorDot(A, B);
	const uniform float Bias = select(DotResult >= 0.f, 1.0f, -1.0f);
	return (B * Alpha) + (A * (Bias * (1.f - Alpha)));
}

// A and B are quaternions.  The result is A + (|A.B| >= 0 ? 1 : -1) * B
inline uniform FVector4 VectorAccumulateQuaternionShortestPath(const uniform FVector4& A, const uniform FVector4& B)
{
	// Blend rotation
	//     To ensure the 'shortest route', we make sure the dot product between the both rotations is positive.
	//     const float Bias = (|A.B| >= 0 ? 1 : -1)
	//     return A + B * Bias;
	const uniform FVector4 RotationDot = VectorDot4(A, B);
	const uniform FVector4 QuatRotationDirMask = VectorCompareGE(RotationDot, FloatZero);
	const uniform FVector4 NegativeB = FloatZero - B;
	const uniform FVector4 BiasTimesB = VectorSelect(QuatRotationDirMask, B, NegativeB);
	return A + BiasTimesB;
}

/**
 * Rotate a vector using a unit Quaternion.
 *
 * @param Quat Unit Quaternion to use for rotation.
 * @param VectorW0 Vector to rotate. W component must be zero.
 * @return Vector after rotation by Quat.
 */
inline FVector4 VectorQuaternionRotateVector(const FVector4& Quat, const FVector4& VectorW0)
{
	// Q * V * Q.Inverse
	//const VectorRegister InverseRotation = VectorQuaternionInverse(Quat);
	//const VectorRegister Temp = VectorQuaternionMultiply2(Quat, VectorW0);
	//const VectorRegister Rotated = VectorQuaternionMultiply2(Temp, InverseRotation);

	// Equivalence of above can be shown to be:
	// http://people.csail.mit.edu/bkph/articles/Quaternions.pdf
	// V' = V + 2w(Q x V) + (2Q x (Q x V))
	// refactor:
	// V' = V + w(2(Q x V)) + (Q x (2(Q x V)))
	// T = 2(Q x V);
	// V' = V + w*(T) + (Q x T)

	const FVector4 QW = VectorReplicate(Quat, 3);
	FVector4 T = VectorCross(Quat, VectorW0);
	T = VectorAdd(T, T);
	const FVector4 VTemp0 = VectorMultiplyAdd(QW, T, VectorW0);
	const FVector4 VTemp1 = VectorCross(Quat, T);
	const FVector4 Rotated = VectorAdd(VTemp0, VTemp1);
	return Rotated;
}

inline uniform FVector4 VectorQuaternionRotateVector(const uniform FVector4& Quat, const uniform FVector4& VectorW0)
{
	// Q * V * Q.Inverse
	//const VectorRegister InverseRotation = VectorQuaternionInverse(Quat);
	//const VectorRegister Temp = VectorQuaternionMultiply2(Quat, VectorW0);
	//const VectorRegister Rotated = VectorQuaternionMultiply2(Temp, InverseRotation);

	// Equivalence of above can be shown to be:
	// http://people.csail.mit.edu/bkph/articles/Quaternions.pdf
	// V' = V + 2w(Q x V) + (2Q x (Q x V))
	// refactor:
	// V' = V + w(2(Q x V)) + (Q x (2(Q x V)))
	// T = 2(Q x V);
	// V' = V + w*(T) + (Q x T)

	const uniform FVector4 QW = VectorReplicate(Quat, 3);
	uniform FVector4 T = VectorCross(Quat, VectorW0);
	T = VectorAdd(T, T);
	const uniform FVector4 VTemp0 = VectorMultiplyAdd(QW, T, VectorW0);
	const uniform FVector4 VTemp1 = VectorCross(Quat, T);
	const uniform FVector4 Rotated = VectorAdd(VTemp0, VTemp1);
	return Rotated;
}

inline uniform FVector VectorQuaternionRotateVector(const uniform FVector4& Quat, const uniform FVector& V)
{
	// http://people.csail.mit.edu/bkph/articles/Quaternions.pdf
	// V' = V + 2w(Q x V) + (2Q x (Q x V))
	// refactor:
	// V' = V + w(2(Q x V)) + (Q x (2(Q x V)))
	// T = 2(Q x V);
	// V' = V + w*(T) + (Q x T)

	const uniform FVector Q = SetVector(Quat.V[0], Quat.V[1], Quat.V[2]);
	const uniform FVector T = VectorCross(Q, V) * 2.f;
	const uniform FVector Result = V + (T * Quat.V[3]) + VectorCross(Q, T);
	return Result;
}

inline FVector VectorQuaternionRotateVector(const uniform FVector4& Quat, const FVector& V)
{
	// http://people.csail.mit.edu/bkph/articles/Quaternions.pdf
	// V' = V + 2w(Q x V) + (2Q x (Q x V))
	// refactor:
	// V' = V + w(2(Q x V)) + (Q x (2(Q x V)))
	// T = 2(Q x V);
	// V' = V + w*(T) + (Q x T)

	const uniform FVector Q = SetVector(Quat.V[0], Quat.V[1], Quat.V[2]);
	const FVector T = VectorCross(Q, V) * 2.f;
	const FVector Result = V + (T * Quat.V[3]) + VectorCross(Q, T);
	return Result;
}

inline uniform FVector8 VectorQuaternionRotateVector(const uniform FVector8& Quat, const uniform FVector8& VectorW0)
{
	const uniform FVector8 QW = VectorReplicate(Quat, 3);
	uniform FVector8 T = VectorCross(Quat, VectorW0);
	T = VectorAdd(T, T);
	const uniform FVector8 VTemp0 = VectorMultiplyAdd(QW, T, VectorW0);
	const uniform FVector8 VTemp1 = VectorCross(Quat, T);
	const uniform FVector8 Rotated = VectorAdd(VTemp0, VTemp1);
	return Rotated;
}

inline uniform FVector4 VectorLerpQuat(const uniform FVector4& A, const uniform FVector4& B, const uniform float Alpha)
{
	// Blend rotation
	//     To ensure the 'shortest route', we make sure the dot product between the both rotations is positive.
	//     const float Bias = (|A.B| >= 0 ? 1 : -1)
	//     Rotation = (B * Alpha) + (A * (Bias * (1.f - Alpha)));
	const uniform float OneMinusAlpha = 1.0f - Alpha;

	const uniform FVector4 RotationDot = VectorDot4(A, B);
	const uniform FVector4 QuatRotationDirMask = VectorCompareGE(RotationDot, FloatZero);
	const uniform FVector4 NegativeA = VectorSubtract(FloatZero, A);
	const uniform FVector4 BiasTimesA = VectorSelect(QuatRotationDirMask, A, NegativeA);
	const uniform FVector4 BTimesWeight = VectorMultiply(B, Alpha);
	const uniform FVector4 UnnormalizedResult = VectorMultiplyAdd(BiasTimesA, OneMinusAlpha, BTimesWeight);

	return UnnormalizedResult;
}

inline uniform FVector4 VectorQuaternionEnforceShortestArcWith(const uniform FVector4& A, const uniform FVector4& B)
{
	const uniform float DotResult = VectorDot(B, A);
	const uniform float Bias = select(DotResult >= 0.f, 1.0f, -1.0f);

	return A * Bias;
}

#endif
